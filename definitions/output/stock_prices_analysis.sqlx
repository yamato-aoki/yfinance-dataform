config {
  type: "table",
  schema: "yfinance_analytics",
  name: "stock_prices_analysis",
  bigquery: {
    partitionBy: "date",
    clusterBy: ["ticker_id"]
  }
}

SELECT
  sp.ticker_id,
  sp.date,
  EXTRACT(YEAR FROM sp.date) AS year,
  EXTRACT(MONTH FROM sp.date) AS month,
  t.company_name,
  s.sector_name,
  c.currency_name,
  sp.open_price,
  sp.high_price,
  sp.low_price,
  sp.close_price,
  sp.volume,
  ROUND(AVG(sp.close_price) OVER (
    PARTITION BY sp.ticker_id ORDER BY sp.date
    ROWS BETWEEN 4 PRECEDING AND CURRENT ROW
  ), 2) AS MA5,
  ROUND(AVG(sp.close_price) OVER (
    PARTITION BY sp.ticker_id ORDER BY sp.date
    ROWS BETWEEN 24 PRECEDING AND CURRENT ROW
  ), 2) AS MA25,
  ROUND(AVG(sp.close_price) OVER (
    PARTITION BY sp.ticker_id ORDER BY sp.date
    ROWS BETWEEN 74 PRECEDING AND CURRENT ROW
  ), 2) AS MA75,
  SAFE_DIVIDE(
    sp.close_price - LAG(sp.close_price) OVER (PARTITION BY sp.ticker_id ORDER BY sp.date),
    LAG(sp.close_price) OVER (PARTITION BY sp.ticker_id ORDER BY sp.date)
  ) AS change_rate
FROM
  ${ref("stock_prices")} AS sp
JOIN
  ${ref("tickers")} AS t ON sp.ticker_id = t.ticker_id
LEFT JOIN
  ${ref("sectors")} AS s ON t.sector_id = s.sector_id
LEFT JOIN
  ${ref("currencies")} AS c ON t.currency_id = c.currency_id
